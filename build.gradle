plugins {
    id 'application'
    id 'org.springframework.boot' version '2.2.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'com.github.johnrengelman.shadow' version '4.0.2'
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
}

group 'io.github.hotlava03'
version '2.0.0-CANARY'
mainClassName = 'io.github.hotlava03.baclava.Main'
compileJava.options.encoding = 'UTF-8'

sourceCompatibility = 1.11

repositories {
    jcenter()
    mavenCentral()
}

configurations {
    ktlint
}

dependencies {
    implementation('net.dv8tion:JDA:4.2.0_216') {
        exclude module: 'opus-java'
    }
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.session:spring-session-core'
    implementation 'org.codehaus.groovy:groovy-jsr223:3.0.0-beta-1'
    implementation 'com.google.code.gson:gson:2.8.1'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-script-util'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    ktlint 'com.pinterest:ktlint:0.39.0'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}
